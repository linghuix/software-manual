/BATCH  
/COM,ANSYS RELEASE 14.0    UP20111024       10:24:59    11/22/2012
/PREP7  
/TITLE, DYNAMIC LOAD EFFECT ON SIMPLY-SUPPORTED THICK SQUARE PLATE  
! 定义单元类型
ET,1,SHELL281   
! 定义厚度  
SECTYPE,1,SHELL 
SECDATA,1,1,0,5 
! 定义材料属性
MP,EX,1,200E9   
MP,NUXY,1,0.3   
MP,ALPX,1,0.1E-5
MP,DENS,1,8000  
! 定义模型  
N,1,0,0,0   
N,9,0,10,0  
FILL
NGEN,5,40,1,9,1,2.5 
N,21,1.25,0,0   
N,29,1.25,10,0  
FILL,21,29,3
NGEN,4,40,21,29,2,2.5   
EN,1,1,41,43,3,21,42,23,2   
EGEN,4,2,1  
EGEN,4,40,1,4   
FINISH  
/SOLU   
ANTYPE,MODAL
! 定义分析类型为模态分析 
MODOPT,REDUC
MXPAND,16,,,YES 
SFE,ALL,,PRES,,-1E6 
! 施加面载荷   
D,ALL,UX,0,,,,UY,ROTZ   
! 施加约束  
D,1,UZ,0,0,9,1,ROTX 
D,161,UZ,0,0,169,1,ROTX 
D,1,UZ,0,0,161,20,ROTY  
D,9,UZ,0,0,169,20,ROTY  
NSEL,S,LOC,X,.1,9.9 
NSEL,R,LOC,Y,.1,9.9 
M,ALL,UZ
! 选择主自由度
NSEL,ALL
SOLVE   
*GET,F,MODE,1,FREQ  
FINISH  
/SOLU   
ANTYPE,SPECTR   
! 定义分析类型
SPOPT,PSD,2,ON  
! 利用前两阶模态并计算应力  
PSDUNIT,1,PRES  
! 定义功率谱为面载荷谱
DMPRAT,0.02 
PSDFRQ,1,1,1.0,80.0 
PSDVAL,1,1.0,1.0
LVSCALE,1   
! 比例使用载荷因子  
PFACT,1,NODE
PSDRES,DISP,REL 
PSDCOM  
SOLVE   
FINISH  
